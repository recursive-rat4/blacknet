buildscript {
    ext.kotlin_version = '1.3.31'
    ext.serialization_version = '0.11.0'
    ext.ktor_version = '1.2.0'

    repositories {
        jcenter()
        maven { url "https://dl.bintray.com/kotlin/kotlinx" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'kotlinx-serialization'
apply plugin: 'application'

mainClassName = "ninja.blacknet.Main"

group 'ninja.blacknet'
version '0.2.0'

repositories {
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/ktor" }
    maven { url "https://dl.bintray.com/kotlin/kotlinx" }
    maven { url "https://dl.bintray.com/ethereum/maven" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "io.ktor:ktor-network:$ktor_version"
    compile "io.ktor:ktor-server-cio:$ktor_version"
    compile "io.ktor:ktor-websockets:$ktor_version"
    compile "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serialization_version"
    compile "net.i2p.crypto:eddsa:0.3.0"
    compile "com.rfksystems:blake2b:1.0.0"
    compile 'org.iq80.leveldb:leveldb:0.10'
    compile "org.slf4j:slf4j-jdk14:1.7.25"
    compile 'io.github.microutils:kotlin-logging:1.6.26'
    compile 'org.briarproject:jtorctl:0.3'
    compile 'com.natpryce:konfig:1.6.10.0'
    compile 'com.google.guava:guava:27.1-jre'
    compile 'org.bitlet:weupnp:0.1.4'
    compile 'org.bouncycastle:bcprov-jdk15on:1.61'
    compile 'org.ethereum:leveldbjni-all:1.18.3'
}

distributions {
    main {
        contents {
            from { "LICENSE.txt" }
            from { "3RD-PARTY-LICENSES.txt" }
        }
    }
}

task createVersionTxt {
    dependsOn processResources
    doLast {
        new File("$buildDir/resources/main/version.txt").write(project.version.toString())
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.freeCompilerArgs += ["-Xuse-experimental=kotlin.ExperimentalUnsignedTypes"]
}

classes {
    dependsOn createVersionTxt
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
